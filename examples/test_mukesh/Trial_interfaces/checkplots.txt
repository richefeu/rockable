set terminal png size 1000,700
set output 'checkplots.png'
set xtics border nomirror
set ytics border nomirror
set mytics 1
set my2tics 1
set multiplot
set origin 0.0, 0.5
set size 0.5, 0.5
set xlabel 'Time'
set y2tics
set ylabel 'Efficiency'
set y2label 'Spent time [%]'
plot 'perf.txt' u 1:2 w l axes x1y1 lw 1.5  lc rgb '#000000' t 'efficiency', \
'' u 1:3 w l axes x1y2 lw 1.5 lc rgb '#FF0000' t 'Neighbor List', \
'' u 1:4 w l axes x1y2 lw 1.5 lc rgb '#0000FF' t 'Forces'

set origin 0.5, 0.5
set size 0.5, 0.5
set xlabel 'Time'
set y2tics
set ylabel 'E_{translation}'
set y2label 'E_{rotation}'
set log y
set log y2
set format y '10^{%L}'
set format y2 '10^{%L}'
plot 'kineticEnergy.txt' u 1:2 w l axes x1y1 lw 1.5 lc rgb '#FF0000' t 'E_{trans}', \
'' u 1:3 w l axes x1y2 lw 1.5 lc rgb '#0000FF' t 'E_{rot}'

set origin 0.0, 0.0
set size 1.0, 0.5
set xlabel 'Time'
set y2tics
set ylabel 'F_{res}^{max} / f_n^{max}'
set y2label 'F_{res}^{max} / f_n^{mean}'
set log y
set log y2
set format y '10^{%L}'
set format y2 '10^{%L}'
plot 'staticBalance.txt' u 1:2 w l axes x1y1 lw 1.5 lc rgb '#FF0000' t '/max', \
'' u 1:3 w l axes x1y2 lw 1.5 lc rgb '#0000FF'  t '/mean'

unset multiplot
